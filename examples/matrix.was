# matrix data can be entered in-line
MATRIX A  COLS 4 ROWS 3  DATA 1 2 3 4 5 6 7 8 9 10 11 12

PRINT TEXT "wasora uses row-major order for matrices (as in C):"
PRINT %g A

# matrix sizes can be defined in terms of expressions involving variables
# that are defined at run-time (i.e. not necessarily constants)
# to illustrate this point, the number N is read from a text file
# during execution. Change the file N.txt and check the result 
VAR N
READ ASCII_FILE_PATH N.txt N
MATRIX B ROWS N-2 COLS N-2 

PRINT
PRINT TEXT "matrix elements may be set as a function of indices i and j"
B(i,j) = 1/(i+j-1)
PRINT %.4f B

MATRIX C COLS N ROWS N   # the keywords COLS and ROWS can be given in any order

PRINT
PRINT TEXT "matrix expressions of i and j can also appear in left hand of the assignment"
PRINT TEXT "explicit ranges with <i_min:i_max;j_min:j_max> can be given to avoid out-of-bound problems:"
C(i,i) = 2
C(i,i-1)<2:N;1:N> = -1
C(i,i+1)<1:N-1;1:N> = -1
C(1,1) = 3
C(N,N) = 3

PRINT "% g" C
